plugins {
    id "java"
    id "java-library"
    id "maven-publish"
    id "signing"
    id "checkstyle"
}

ext {
    if (!project.hasProperty("jacksonVersionForJacksonTest")) {
        jacksonVersionForJacksonTest = "2.15.3"
    }
}

repositories {
    mavenCentral()
}

group = "org.embulk"
version = "0.4.1-SNAPSHOT"
description = "Embulk configuration processor for Embulk plugins"

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()

    legacyTestImplementation {
        extendsFrom testImplementation
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.encoding = "UTF-8"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }

    withJavadocJar()
    withSourcesJar()
}

sourceSets {
    legacyTest {
        java {
            srcDir "src/legacyTest/java"
        }
        resources {
            srcDir "src/legacyTest/resources"
        }
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

dependencies {
    compileOnly "org.embulk:embulk-spi:0.11"
    compileOnly "org.slf4j:slf4j-api:2.0.7"

    // Dependencies should be "api" so that their scope would be "compile" in "pom.xml".
    api "javax.validation:validation-api:1.1.0.Final"
    api platform("com.fasterxml.jackson:jackson-bom:2.15.3")
    api "com.fasterxml.jackson.core:jackson-annotations"
    api "com.fasterxml.jackson.core:jackson-core"
    api "com.fasterxml.jackson.core:jackson-databind"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"  // Required for java.util.Optional.

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.10.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.0"

    testImplementation "org.embulk:embulk-spi:0.11"

    testImplementation "org.apache.bval:bval-jsr303:0.5"
    testImplementation "joda-time:joda-time:2.9.2"
    testImplementation "ch.qos.logback:logback-classic:1.3.6"

    testImplementation platform("com.fasterxml.jackson:jackson-bom:$jacksonVersionForJacksonTest")
    testImplementation "com.fasterxml.jackson.core:jackson-annotations"
    testImplementation "com.fasterxml.jackson.core:jackson-core"
    testImplementation "com.fasterxml.jackson.core:jackson-databind"
    testImplementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"  // Required for java.util.Optional.

    legacyTestImplementation "junit:junit:4.13.2"

    legacyTestImplementation "org.embulk:embulk-core:0.11.1"
    legacyTestImplementation "org.embulk:embulk-deps:0.11.1"
    legacyTestImplementation "org.embulk:embulk-junit4:0.11.1"
    legacyTestImplementation "ch.qos.logback:logback-classic:1.3.6"

    legacyTestImplementation platform("com.fasterxml.jackson:jackson-bom:$jacksonVersionForJacksonTest")
    legacyTestImplementation "com.fasterxml.jackson.core:jackson-annotations"
    legacyTestImplementation "com.fasterxml.jackson.core:jackson-core"
    legacyTestImplementation "com.fasterxml.jackson.core:jackson-databind"
    legacyTestImplementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"  // Required for java.util.Optional.
}

javadoc {
    title = "${project.name} v${project.version}"

    // "*NodeRebuilder" classes are unofficial and undocumented while ObjectNodeRebuilder is "public".
    exclude "org/embulk/util/config/rebuild/*.java"

    // "DataSourceImpl" is unofficial and undocumented while it is "public".
    exclude "org/embulk/util/config/DataSourceImpl.java"

    options {
        locale = "en_US"
        encoding = "UTF-8"
        overview = "src/main/html/overview.html"
        links "https://docs.oracle.com/javase/8/docs/api/"
        links "https://docs.oracle.com/javaee/7/api/"
        links "https://dev.embulk.org/embulk-spi/0.11/javadoc/"
        links "https://fasterxml.github.io/jackson-core/javadoc/2.6/"
        links "https://fasterxml.github.io/jackson-databind/javadoc/2.6/"
        links "https://fasterxml.github.io/jackson-datatype-jdk8/javadoc/2.6/"
    }
}

jar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

sourcesJar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

javadocJar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

// A safer and strict alternative to: "dependencies" (and "dependencies --write-locks")
//
// This task fails explicitly when the specified dependency is not available.
// In contrast, "dependencies (--write-locks)" does not fail even when a part the dependencies are unavailable.
//
// https://docs.gradle.org/7.6.1/userguide/dependency_locking.html#generating_and_updating_dependency_locks
task checkDependencies {
    notCompatibleWithConfigurationCache("The task \"checkDependencies\" filters configurations at execution time.")
    doLast {
        configurations.findAll { it.canBeResolved }.each { it.resolve() }
    }
}

// It should not publish a `.module` file in Maven Central.
// https://docs.gradle.org/current/userguide/publishing_gradle_module_metadata.html#sub:disabling-gmm-publication
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            from components.java
            // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
            // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                name = project.name
                description = project.description
                url = "https://www.embulk.org/"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "repo"
                    }
                }

                developers {
                    developer {
                        name = "Dai MIKURUBE"
                        email = "dmikurube@acm.org"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/embulk/embulk-util-config.git"
                    developerConnection = "scm:git:git@github.com:embulk/embulk-util-config.git"
                    url = "https://github.com/embulk/embulk-util-config"
                }

                withXml {
                    project.configurations.compileOnly.allDependencies.each { dependency ->
                        asNode().dependencies[0].appendNode("dependency").with {
                            it.appendNode("groupId", dependency.group)
                            it.appendNode("artifactId", dependency.name)
                            it.appendNode("version", dependency.version)
                            it.appendNode("scope", "provided")
                        }
                    }
                }
            }
        }
    }

    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    if (project.hasProperty("signingKey") && project.hasProperty("signingPassword")) {
        logger.lifecycle("Signing with an in-memory key.")
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
    sign publishing.publications.maven
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        outputs.upToDateWhen { false }
    }
    systemProperty "org.embulk.embulk_util_config.version", "${project.version}"
}

task legacyTest(type: Test, description: "Tests compatible with legacy Embulk DataSource.", group: "Verification") {
    classpath = sourceSets.legacyTest.runtimeClasspath
    testClassesDirs = sourceSets.legacyTest.output.classesDirs

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        outputs.upToDateWhen { false }
    }
}

check {
    dependsOn "legacyTest"
}

tasks.withType(Checkstyle) {
    reports {
        // Not to skip up-to-date checkstyles.
        outputs.upToDateWhen { false }
    }
}

checkstyle {
    toolVersion "8.7"
    configFile = file("${projectDir}/config/checkstyle/checkstyle.xml")
    configProperties = [
        "checkstyle.config.path": file("${projectDir}/config/checkstyle")
    ]
    ignoreFailures = false
    maxWarnings = 0  // https://github.com/gradle/gradle/issues/881
}
